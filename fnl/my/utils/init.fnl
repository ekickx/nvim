(local export {})

(macro api! [api ...]
  "(api! get_option_value :buflisted {:buf bufnr})
  will produce this lua code:
  vim.api.nvim_get_option_value('buflisted', {buf = bufnr})"
  (let [apifn (.. :vim.api.nvim_ (. api 1))]
    (list (sym apifn) ...)))

(fn export.packadd! [package]
  (vim.cmd (.. "packadd " package)))

(fn export.map! [mode key run opt]
  "
  `(map! :mode :key :run :desc)`
  or
  `(map! :mode :key :run opt)`

  Example:
  `(map! :nv :<leader>k ::q :quit)`
  "
  (let [mode (vim.split mode "")
        opt (if (= :string (type opt)) {:desc opt} opt)]
    (vim.keymap.set mode key run opt)))

(fn export.cmd! [name run opt]
  "
  `(cmd! :name run)`
  or
  `(cmd! :name run :desc)`
  or
  `(cmd! :name run opt)`
  "
  (let [opt (if (= :nil (type opt)) {}
              (if (= :string (type opt)) {:desc opt} opt))]
    (api! create_user_command name run opt)))

(fn autocmd! [events pattern callback]
  (let [pattern (if (= :string (type pattern)) [pattern] pattern)
        callback (if (= :string (type callback)) #(vim.cmd callback) callback)]
  (api! create_autocmd events {:pattern pattern :callback callback})))

(fn export.augroup! [name ...]
  "
  `(augroup! :name
  [{event} {pattern} {callback}]
  [{event} {pattern} {callback}])`
  "

  (let [group (api! create_augroup name {:clear true})]
    (each [_ [event pattern callback] (ipairs ...)]
      (api! create_autocmd event {: pattern
                                  :callback (if (= :string (type callback))
                                              #(vim.cmd callback)
                                              callback)
                                  : group}))))

(fn export.z [query]
  (let [output  (: (vim.system [:zoxide :query query]) :wait)
        path    (. (vim.split output.stdout "\n") 1)]
    (if (= 0 output.code)
      (do
      (vim.cmd.tcd path)
      (vim.system [:zoxide :add path])))))

export
